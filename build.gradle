plugins {
    id "org.jetbrains.intellij" version "0.2.13"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

// On travis, we don't want to deploy to Jetbrains that requires credentials
if (project.file('gradle.properties').exists()) {
    apply from: "gradle.properties"
}

intellij {
    version = '2017.1.4'
    pluginName = 'Mathematica-IntelliJ-Plugin'
    updateSinceUntilBuild = false
}

// Simple function to load change-notes.html and description.html into valid text for plugin.xml
def htmlFixer = {f -> file(f).text.replace('<html>', '').replace('</html>', '')}

patchPluginXml {
    changeNotes = htmlFixer('resources/META-INF/change-notes.html')
    pluginDescription = htmlFixer('resources/META-INF/description.html')
}


repositories {
    mavenCentral()
}

sourceSets {
    //noinspection GroovyAssignabilityCheck
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['tests']
        resources.srcDirs = ['testData']
    }
}

task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/testData"
    into "${buildDir}/classes/test"
}

instrumentTestCode.dependsOn copyResourcesToClasses

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

version '2.4.3'